Homework 10
Audit 1

---

QUESTION 1
Imagine you have been given the following code to audit:

    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.4;
    import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

    contract DogCoinGame is ERC20 {
        uint256 public currentPrize;
        uint256 public numberPlayers;
        address payable[] public players;
        address payable[] public winners;

        event startPayout();

        constructor() ERC20("DogCoin", "DOG") {}

        function addPlayer(address payable _player) public payable {
            if (msg.value == 1) {
                players.push(_player);
            }
            numberPlayers++;
            if (numberPlayers > 200) {
                emit startPayout();
            }
        }

        function addWinner(address payable _winner) public {
            winners.push(_winner);
        }

        function payout() public {
            if (address(this).balance == 100) {
                uint256 amountToPay = winners.length / 100;
                payWinners(amountToPay);
            }
        }

        function payWinners(uint256 _amount) public {
            for (uint256 i = 0; i <= winners.length; i++) {
                winners[i].send(_amount);
            }
        }
    }

With the following note from the team:

"DogCoinGame is a game where players are added to the
contract via the addPlayer function, they need to send 1 ETH
to play.
Once 200 players have entered, the UI will be notified by the
startPayout event, and will pick 100 winners which will be
added to the winners array, the UI will then call the payout
function to pay each of the winners.
The remaining balance will be kept as profit for the
developers."

Write out the main points that you would include in an audit
report.

---

--REPORT--:

Purpose of the Contract:

Scope of the Audit:

    The Scope was limited to the functionality of the included contract as it stands. There are a total of 4 state variables, 2 being dynamic. There is one event and one constructor. There are an additional 4 functions.

Overview of the Contract:

    The contract is designed around the ERC20 token standard and uses the openZeppelin library. The contract uses the name DogCoin and the symbol $DOG in the constructor.

    Players can join the game bu calling the addPlayer function and sending 1 ether.
    The player is then added to the players array and the numberPlayer is increased by one.
    When the numberPlayers is greater than 200 the startPayout event is emmited.

    Winners are added to the winners array by calling the addWinner function.

    The payout function checks if the contract balance is greater than 100, and then calculates the amount to pay each winner by diving winners by 100 and calling the payWinners function.

    The payWinners function will iterate though the winners array and using send, distribute the ether to the winners.

Analysis:

    The contract was compiled in a foundary environment with all additional tests being conducted in solidity.

    Some of the issues encountered were:

    1 - Instead of .send use the transfer function in the line: winners[i].send(_amount); This will result in additional security checks.

    2 - The numberPlayers is increamented no matter the value sent and could be increased without sending the proper amount.

    3 - The currentPrize variable is not used or updated.

    4 - The payWinners function could be off by one by using i <= winners.length and not i < winners.length

    5- In the payout function the amountToPay could be 0 if there are less than 100 winners.

    6 - There is no Access Control and anyone can call the functions, addWinner, payout, or payWinners.

---

Repository for the Audit: (currently private)

    https://github.com/ecorey/Audit1

---

QUESTION 2
DIFFICULT
Underhanded Solidity
Meant to look benign but is not...

This contract is the winner of this years underhanded solidity
contest, it mimics the OpenSea application.
Can you spot the flaws in it
â›½ðŸŒThis is a gas-golfed version of Zora v3's Offers module!
ðŸ¤© A bidder can call createBid to bid on the NFT of their
dreams.
ðŸ’° The NFT owner can call acceptBid to accept one of these
on-chain bids.
ðŸ¤ Assets exchange hands.
ðŸ˜¤ What could possibly go wrong?

    // SPDX-License-Identifier: Unlicense
    pragma solidity ^0.8;

    import "solmate/tokens/ERC20.sol";
    import "solmate/tokens/ERC721.sol";
    import "solmate/utils/SafeTransferLib.sol";

    contract BrokenSea {
        using SafeTransferLib for ERC20;

        // Bidder => asset pair key => NFT token ID => bid
        mapping(address => mapping(uint160 => mapping(uint256 => uint256))) bids;

        /// @dev Creates an bid for the given NFT. Can also be used to
        ///      update the price of an existing bid, or cancel a bid by
        ///      providing price = 0.
        /// @param erc721Token The ERC721 token contract.
        /// @param erc721TokenId The ID of the ERC721 asset to sell.
        /// @param erc20Token The ERC20 token contract.
        /// @param price The bid price, denominated in the given ERC20 token.
        function createBid(
            ERC721 erc721Token,
            uint256 erc721TokenId,
            ERC20 erc20Token,
            uint256 price
        ) external {
            uint160 key = _getKey(erc20Token, erc721Token);
            bids[msg.sender][key][erc721TokenId] = price;
        }

        /// @dev Accepts a bid on the caller's NFT. Transfers the
        ///      ERC721 asset to the bidder, and transfers ERC20 tokens
        ///      from the bidder to the caller.
        /// @param bidder The address that created the bid.
        /// @param erc721Token The ERC721 token contract.
        /// @param erc721TokenId The ID of the ERC721 asset to sell.
        /// @param erc20Token The ERC20 token contract.
        /// @param price The price the caller is willing to accept.
        ///        Reverts if the bid price is less than this amount.
        function acceptBid(
            address bidder,
            ERC721 erc721Token,
            uint256 erc721TokenId,
            ERC20 erc20Token,
            uint256 price
        ) external {
            uint160 key = _getKey(erc20Token, erc721Token);
            uint256 bidPrice = bids[bidder][key][erc721TokenId];
            // If the bid price is 0, either the bid hasn't been
            // created yet or it has been cancelled.
            require(bidPrice != 0, "BrokenSea::fillBid/BID_PRICE_ZERO");
            // Check that the bid price is at least the taker's price.
            // This prevents the bidder from front-running the fill and
            // lowering the price.
            require(bidPrice >= price, "BrokenSea::fillBid/BID_TOO_LOW");

            // Mark bid as filled before performing transfers.
            delete bids[bidder][key][erc721TokenId];

            // solmate's SafeTransferLib uses a low-level call, so we
            // need to manually check that the contract exists.
            uint256 size;
            assembly {
                size := extcodesize(erc20Token)
            }
            require(size > 0, "BrokenSea::fillBid/NO_CODE");
            erc20Token.safeTransferFrom(bidder, msg.sender, price);

            // Since this is _not_ a low-level call, the Solidity
            // compiler will insert an `extcodesize` check like the one
            // above; no need to do it ourselves here.
            // Reverts if the caller does not own the NFT.
            erc721Token.transferFrom(msg.sender, bidder, erc721TokenId);
        }

        // The `bids` storage mapping could be keyed by erc20Token and
        // erc721Token individually, i.e.
        //     bids[bidder][erc20Token][erc721Token][erc721TokenId]
        // but that would require computing 4 keccak256 hashes per read/write.
        // As a minor gas optimization, the `bids` storage mapping is instead
        // keyed by the XOR of the two token addresses, i.e.
        //     bids[bidder][erc20Token ^ erc721Token][erc721TokenId]
        // It is statistically impossible to farm contract addresses that would
        // create a key collision.
        function _getKey(ERC20 erc20Token, ERC721 erc721Token)
            private
            pure
            returns (uint160 key)
        {
            return uint160(address(erc20Token)) ^ uint160(address(erc721Token));
        }
    }

---

Hints
Look at the Solmate contracts used, and the way
transferFrom is implemented.

    https://github.com/transmissions11/solmate
